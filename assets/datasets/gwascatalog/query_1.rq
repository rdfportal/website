# Endpoint: https://togovar.org/sparql
# Description: GWAS Catalog association and study record for dbsnp rs671 variant
# Parameter: snps: (example: rs671)

PREFIX gwas: <http://rdf.ebi.ac.uk/terms/gwas/>
PREFIX terms: <http://med2rdf.org/gwascatalog/terms/>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT ?strongest_snp_risk_allele ?p_value ?beta ?beta_unit ?ci_text ?mapped_trait_text ?mapped_trait_uri ?refpubmed ?study ?study_description ?initial_sample_size ?replication_sample_size ?snps
WHERE {
    VALUES ?snps { "rs671" }
    ?GWASAssociation a gwas:Association ;
        terms:strongest_snp_risk_allele ?strongest_snp_risk_allele ;
        terms:p_value ?p_value ;
        terms:beta ?beta ;
        terms:beta_unit ?beta_unit ;
        terms:ci_text ?ci_text ;
        terms:mapped_trait ?mapped_trait_text ;
        terms:mapped_trait_uri ?mapped_trait_uri ;
        dct:references ?refpubmed ;
        terms:study ?study ;
        terms:snps ?snps .
    ?study a gwas:Study ;
        dct:description ?study_description ;
        terms:initial_sample_size ?initial_sample_size ;
        terms:replication_sample_size ?replication_sample_size .
}
LIMIT 100
