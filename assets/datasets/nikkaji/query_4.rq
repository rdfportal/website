### 04 ###
# Add information of DBpedia subjects to Nikkaji compounds by using InChIKey

define input:same-as "yes"
PREFIX jst: <http://vocab.jst.go.jp/terms/sti#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ssc: <http://semanticscience.org/resource/>
PREFIX dbp: <http://dbpedia.org/property/>

select distinct ?nikkaji ?NikkajiNum ?Label ?InChIKey ?dbpedia ?Subject

where 
{
	SERVICE <http://live.dbpedia.org/sparql>{
	?dbpedia dct:subject ?Subject ;
	dbp:stdinchikey ?InChIKey_tag BIND (STR(?InChIKey_tag) as ?InChIKey) .
	}

	GRAPH <http://rdfportal.org/dataset/nikkaji/core> {
	?nikkaji rdf:type jst:Chemical ;
		dct:identifier ?NikkajiNum ;
		rdfs:label ?Label filter (lang(?Label)="en") .
	}

	GRAPH <http://rdfportal.org/dataset/nikkaji/inchi> {
	?nikkaji ssc:CHEMINF_000200 ?atr_inchikey .
	?atr_inchikey rdf:type ssc:CHEMINF_000399 ;
		ssc:SIO_000300 ?InChIKey .
	}

} 
LIMIT 100

