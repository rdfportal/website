# Endpoint: https://rdfportal.org/primary/sparql
# Description: RDFized reference gene expresson dataset derived from CAGE and GeneChip experiments in the RefEx database.

PREFIX refexo: <http://purl.jp/bio/01/refexo#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?refex_id ?ex_value ?see_also ?RefExSample ?sample_id ?taxonomy ?age ?stage ?description ?cell_type ?disease ?dev_site ?refexo_sample
FROM <http://refex.dbcls.jp/rdf/fantom5>
FROM <http://refex.dbcls.jp/rdf/genechip>
FROM <http://purl.jp/bio/01/refexo>
WHERE {
    ?RefExEntry a refexo:RefExEntry ;
        dct:identifier ?refex_id ;
        refexo:expValue ?ex_value ;
        rdfs:seeAlso ?see_also ;
        refexo:refexSample ?RefExSample .
    ?RefExSample a refexo:RefExSample ;
        dct:identifier ?sample_id .
    OPTIONAL {
        ?RefExSample refexo:organism ?taxonomy .
    }
    OPTIONAL {
        ?RefExSample refexo:age ?age .
    }
    OPTIONAL {
        ?RefExSample refexo:developmentalStage ?stage .
    }
    OPTIONAL {
        ?RefExSample refexo:refexRefinedDescription ?description .
    }
    OPTIONAL {
        ?RefExSample refexo:sampleReference / refexo:belongsToCellType ?cell_type .
    }
    OPTIONAL {
        ?RefExSample refexo:sampleReference / refexo:belongsToDisease ?disease .
    }
    OPTIONAL {
        ?RefExSample refexo:sampleReference / refexo:belongsToDevelopmentSite ?dev_site .
    }
    OPTIONAL {
        ?RefExSample refexo:sampleReference / refexo:sample ?refexo_sample .
    }
}
LIMIT 100
