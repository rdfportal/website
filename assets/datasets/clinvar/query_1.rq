# Endpoint: https://rdfportal.org/ncbi/sparql
# Description: Obrain clinical annotations from ClinVar
# Parameter: reference: (example: <http://identifiers.org/hco/13#GRCh37>)
#            position: (example: 32889080)
#            db: (example: MedGen)

PREFIX cvo: <http://purl.jp/bio/10/clinvar/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>

SELECT ?vcv ?interpretation ?id ?position ?reference
FROM <http://rdfportal.org/dataset/clinvar>
WHERE {
    VALUES ?reference { <http://identifiers.org/hco/13#GRCh37> }
    VALUES ?position { 32889080 }
    ?Clinvar a cvo:VariationArchiveType ;
        cvo:accession ?vcv ;
        cvo:variation_id ?id ;
        cvo:classified_record / sio:SIO_000628 / faldo:location / faldo:position ?position ;
        cvo:classified_record / sio:SIO_000628 / faldo:location / faldo:reference ?reference .
    OPTIONAL {
        ?Clinvar cvo:classified_record / cvo:rcv_list / cvo:rcv_accession / cvo:rcv_classifications / cvo:germline_classification / cvo:description / cvo:description ?interpretation .
    }
}
LIMIT 100
