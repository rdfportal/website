# Endpoint: https://rdfportal.org/primary/sparql
# Description: Obrain gene records from the HGNC data

PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX m2r: <http://med2rdf.org/ontology/med2rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX idt: <http://identifiers.org/>

SELECT ?gene_id ?hgnc_id ?description ?location ?ncbigene_id ?ensembl_id ?uniprot_id ?pubmed_id
FROM <http://rdfportal.org/dataset/hgnc>
WHERE {
    VALUES ?HGNC__class { obo:SO_0000704 m2r:Gene }
    ?HGNC a ?HGNC__class ;
        rdfs:label ?gene_id ;
        dct:identifier ?hgnc_id ;
        dct:description ?description .
    OPTIONAL {
        ?HGNC obo:so_part_of ?location .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?NCBIGene .
        ?NCBIGene a idt:ncbigene ;
            dct:identifier ?ncbigene_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?Ensembl .
        ?Ensembl a idt:ensembl ;
            dct:identifier ?ensembl_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?UniProt .
        ?UniProt a idt:uniprot ;
            dct:identifier ?uniprot_id .
    }
    OPTIONAL {
        ?HGNC dct:references / dct:identifier ?pubmed_id .
    }
}
LIMIT 100
