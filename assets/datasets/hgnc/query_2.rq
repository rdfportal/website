# Endpoint: https://rdfportal.org/primary/sparql
# Description: Obtain a gene record from the HGNC data
# Parameter: gene_id: (example: TP53)

PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX m2r: <http://med2rdf.org/ontology/med2rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX idt: <http://identifiers.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?gene_id ?description ?see_also ?reference ?alt_label
FROM <http://rdfportal.org/dataset/hgnc>
WHERE {
    VALUES ?gene_id { "TP53" }
    VALUES ?HGNC__class { obo:SO_0000704 m2r:Gene }
    ?HGNC a ?HGNC__class ;
        rdfs:label ?gene_id ;
        dct:description ?description .
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?see_also .
    }
    OPTIONAL {
        ?HGNC dct:references ?reference .
        ?reference a idt:pubmed .
    }
    OPTIONAL {
        ?HGNC skos:altLabel ?alt_label .
    }
}
LIMIT 100
