# Endpoint: https://rdfportal.org/primary/sparql
# Description: Extract link information from the HGNC data

PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX m2r: <http://med2rdf.org/ontology/med2rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX idt: <http://identifiers.org/>

SELECT ?gene_id ?hgnc_id ?ncbigene_id ?ensembl_id ?ena_id ?insdc_id ?refseq_id ?ccds_id ?uniprot_id ?pubmed_id ?mgi_id ?rgd_id ?lrg_id ?omim_id ?orphanet_id ?mirbase_id ?ec_id
FROM <http://rdfportal.org/dataset/hgnc>
WHERE {
    VALUES ?HGNC__class { obo:SO_0000704 m2r:Gene }
    ?HGNC a ?HGNC__class ;
        rdfs:label ?gene_id ;
        dct:identifier ?hgnc_id .
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?NCBIGene .
        ?NCBIGene a idt:ncbigene ;
            dct:identifier ?ncbigene_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?Ensembl .
        ?Ensembl a idt:ensembl ;
            dct:identifier ?ensembl_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?ENA .
        ?ENA a idt:ena.embl ;
            dct:identifier ?ena_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?INSDC .
        ?INSDC a idt:insdc ;
            dct:identifier ?insdc_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?RefSeq .
        ?RefSeq a idt:refseq ;
            dct:identifier ?refseq_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?CCDS .
        ?CCDS a idt:ccds ;
            dct:identifier ?ccds_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?UniProt .
        ?UniProt a idt:uniprot ;
            dct:identifier ?uniprot_id .
    }
    OPTIONAL {
        ?HGNC dct:references / dct:identifier ?pubmed_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?MGI .
        ?MGI a idt:mgi ;
            dct:identifier ?mgi_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?RGD .
        ?RGD a idt:rgd ;
            dct:identifier ?rgd_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?LRG .
        ?LRG a idt:lrg ;
            dct:identifier ?lrg_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?OMIM .
        ?OMIM a idt:omim ;
            dct:identifier ?omim_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?Orphanet .
        ?Orphanet a idt:orphanet ;
            dct:identifier ?orphanet_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?MiRBase .
        ?MiRBase a idt:mirbase ;
            dct:identifier ?mirbase_id .
    }
    OPTIONAL {
        ?HGNC rdfs:seeAlso ?EC .
        ?EC a idt:ec-code ;
            dct:identifier ?ec_id .
    }
}
LIMIT 100
