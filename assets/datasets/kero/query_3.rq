# Dataset and Sample Metadata
# Note the SPARQL endpoint for KERO is https://rdfportal.org/rdf/kero/sparql .

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX kero: <http://kero.hgc.jp/ontology/kero.owl#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX snomed: <http://purl.bioontology.org/ontology/SNOMEDCT/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX dbponto: <http://dbpedia.org/ontology/>

SELECT DISTINCT ?dataset_type ?dataset ?title ?species ?sample_id ?sample_label ?sample_gender ?sample_age
FROM <http://rdfportal.org/dataset/kero/Cancer_Cell_Line> 
FROM <http://kero.hgc.jp/ontology/kero.owl> 
WHERE {
    ?dataset rdf:type ?dataset_type_uri .
    ?dataset_type_uri rdfs:label ?dataset_type .
    ?dataset_type_uri rdfs:subClassOf sio:SIO_000089 .
    ?dataset rdfs:label ?title .
    ?dataset dcat:theme / rdfs:label ?species .
    ?dataset kero:sample ?sample .
    ?sample dcterms:identifier ?sample_id .
    ?sample rdfs:label ?sample_label .
    OPTIONAL { 
      ?sample snomed:specimenOf ?person . 
      ?person foaf:gender ?sample_gender . 
      OPTIONAL { ?person dbponto:age ?sample_age . }
    }
}

