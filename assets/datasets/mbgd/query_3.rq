######
# Get orthologs and their annotations for a given UniProt ID
#

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX orth: <http://purl.jp/bio/11/orth#>
PREFIX mbgd: <http://purl.jp/bio/11/mbgd#>
PREFIX mbgdr: <http://mbgd.genome.ad.jp/rdf/resource/>
PREFIX uniprot: <http://purl.uniprot.org/uniprot/>

SELECT DISTINCT ?uniprot_id ?mbgd_gene_id ?gene_description ?organism
FROM <http://mbgd.genome.ad.jp/rdf/resource/default>
FROM <http://mbgd.genome.ad.jp/rdf/resource/xref_uniprot>
FROM <http://mbgd.genome.ad.jp/rdf/resource/gene>
FROM <http://mbgd.genome.ad.jp/rdf/resource/organism>
WHERE {
    ?group a orth:OrthologGroup ;
        orth:inDataset mbgdr:default ;
        orth:member/orth:gene/mbgd:uniprot uniprot:K9Z723 ;
        orth:member/orth:gene ?gene .
    ?gene dct:identifier ?mbgd_gene_id ;
        mbgd:uniprot ?uniprot_id ;
        dct:description ?gene_description ;
        orth:organism/dct:description ?organism .
}
ORDER BY ?organism
