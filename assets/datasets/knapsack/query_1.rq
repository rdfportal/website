# Endpoint: https://rdfportal.org/primary/sparql
# Description: Get attribute information for C00000001 in KNApSAcK Core
# Parameter: knapsack_id: (example: C00000001)

PREFIX knapsack: <http://purl.jp/knapsack/resource#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX cheminf: <http://semanticscience.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?KNApSAcKCoreRecord ?knapsack_id ?molecular_entity_name ?molecular_formula ?value_mw ?value_smiles ?value_inchikey ?value_inchi ?rdfs_seealso
FROM <http://purl.jp/knapsack/>
WHERE {
    VALUES ?knapsack_id { "C00000001" }
    VALUES ?KNApSAcKCoreRecord__class { knapsack:KNApSAcKCoreRecord knapsack:KNApSAcKMetaboliteRecord }
    ?KNApSAcKCoreRecord a ?KNApSAcKCoreRecord__class ;
        dc:identifier ?knapsack_id ;
        rdfs:seeAlso ?rdfs_seealso ;
        sio:SIO_000008 ?MolecularEntityName ;
        sio:SIO_000008 ?MolecularFormula ;
        sio:SIO_000008 ?MolecularWeight ;
        cheminf:CHEMINF_000200 ?Smiles ;
        cheminf:CHEMINF_000200 ?StandardInchi ;
        cheminf:CHEMINF_000200 ?StandardInchikey .
    ?MolecularEntityName a cheminf:CHEMINF_000043 ;
        sio:SIO_000300 ?molecular_entity_name .
    ?MolecularFormula a cheminf:CHEMINF_000042 ;
        sio:SIO_000300 ?molecular_formula .
    ?MolecularWeight a cheminf:CHEMINF_000334 ;
        sio:SIO_000300 ?value_mw .
    ?Smiles a cheminf:CHEMINF_000018 ;
        sio:SIO_000300 ?value_smiles .
    ?StandardInchikey a cheminf:CHEMINF_000059 ;
        sio:SIO_000300 ?value_inchikey .
    ?StandardInchi a cheminf:CHEMINF_000113 ;
        sio:SIO_000300 ?value_inchi .
}
LIMIT 100
