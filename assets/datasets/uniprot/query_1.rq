# Endpoint: https://rdfportal.org/sib/sparql
# Description: protein name, mnemonic and gene name of human
# Parameter: taxonomy: (example: taxonomy:9606)

PREFIX core: <http://purl.uniprot.org/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX taxonomy: <http://purl.uniprot.org/taxonomy/>

SELECT DISTINCT ?UniProt ?recommended_name ?submited_name ?mnemonic ?gene_name ?taxonomy
FROM <http://sparql.uniprot.org/uniprot>
FROM <http://sparql.uniprot.org/obsolete>
FROM <http://sparql.uniprot.org/citationmapping>
FROM <http://sparql.uniprot.org/taxonomy>
FROM <http://sparql.uniprot.org/citations>
FROM <http://sparql.uniprot.org/proteomes>
FROM <http://sparql.uniprot.org/go>
FROM <http://sparql.uniprot.org/enzyme>
FROM <http://sparql.uniprot.org/diseases>
FROM <http://sparql.uniprot.org/journal>
FROM <http://sparql.uniprot.org/pathways>
FROM <http://sparql.uniprot.org/keywords>
FROM <http://sparql.uniprot.org/locations>
FROM <http://sparql.uniprot.org/tissues>
FROM <http://sparql.uniprot.org/core>
FROM <http://sparql.uniprot.org/databases>
WHERE {
    VALUES ?taxonomy { taxonomy:9606 }
    ?UniProt a core:Protein ;
        core:mnemonic ?mnemonic ;
        core:organism ?taxonomy .
    OPTIONAL {
        ?UniProt core:recommendedName / core:fullName ?recommended_name .
    }
    OPTIONAL {
        ?UniProt core:submittedName / core:fullName ?submited_name .
    }
    OPTIONAL {
        ?UniProt core:encodedBy / skos:prefLabel ?gene_name .
    }
}
LIMIT 100
