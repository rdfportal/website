# (2)"Pseudomonas putida"が生分解するポリマーの構造基礎名と分子量

prefix biodg: <http://dice.nims.go.jp/ontology/polymer-bio-ont/Biodeg#>
prefix cu: <http://dice.nims.go.jp/ontology/PoLyInfo-ont/polyinfo-rdf/ConstitutionalUnit#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix taxddbj: <http://ddbj.nig.ac.jp/ontologies/taxonomy/>
prefix homo: <http://dice.nims.go.jp/ontology/PoLyInfo-ont/polyinfo-rdf/Homopolymer#>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix pli: <http://dice.nims.go.jp/ontology/PoLyInfo-ont/Schema#>
prefix prism: <http://prismstandard.org/namespaces/basic/2.0/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix ref: <http://dice.nims.go.jp/ontology/polymer-bio-ont/Reference/>
prefix sio: <http://semanticscience.org/resource/>
prefix taxonomy: <http://identifiers.org/taxonomy/>
prefix wdt: <http://dice.nims.go.jp/ontology/polymer-bio-ont/Property#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT distinct ?tax ?sname ?mw_val
WHERE {
  SERVICE <https://rdfportal.org/ddbj/sparql>{
  ?tax taxddbj:scientificName "Pseudomonas putida" ;
    a taxddbj:Taxon .
  }
  ?deg obo:RO_0000057 ?tax;
    obo:RO_0000057 ?poly .
  ?poly a pli:Homopolymer ;
    sio:SIO_000008 ?name .
  ?name a pli:StructureBasedName ;
    sio:SIO_000300 ?sname .
  ?poly sio:SIO_000008 ?cu .
  ?cu a pli:ConstitutionalUnit ;
    obo:BFO_0000051 ?mw .
  ?mw  a pli:MolecularWeight ;
    sio:SIO_000300 ?mw_val .
}

